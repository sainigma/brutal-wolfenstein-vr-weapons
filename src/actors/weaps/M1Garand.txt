Actor M1GARAND:Weapon {
  Weapon.AmmoUse1 0
  Weapon.AmmoGive1 8
  Weapon.AmmoUse2 0
  Weapon.AmmoGive2 0
  Weapon.AmmoType1 "M1CLIP"
  Weapon.AmmoType2 "M1LOAD"
  Weapon.BobRangeX 0.75
  Weapon.BobRangeY 0.5
  Weapon.BobSpeed 1.5
  Weapon.BobStyle "InverseAlpha" //Denis Belmondo's Bobstyle
  AttackSound "None"
  Inventory.PickupSound "MP40_TAKE"
  Inventory.Pickupmessage "You got the M1 Garand"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.NOAUTOAIM
  +WEAPON.NOALERT
  +WEAPON.NOAUTOFIRE
  tag "M1 Garand"
  Inventory.Icon "M1BACK"
  Scale 1.0

  States {

    // Sprites
    // M1GG
    //      A default
    //      J default, slide back
    //      B-E firing cycle
    //      F-I slide return
    //
    // M1GS A-H weapon intro
    //
    // M1GR A-Q reload

    Ready:
      TNT1 A 0 A_PlaySound("Deploy/garand")
      M1GS ABCDEFGH 1 
      Goto RealReady

    RealReady:
      TNT1 A 0 A_JumpIfInventory("M1LOAD",1,1)
      Goto RealReadyEmptyMag

      M1GG A 1 A_WeaponReady
      TNT1 A 0 A_SetCrosshair(0)
      TNT1 A 0 A_JumpIfInventory("ChangeMelee",1,"NewMelee")
      TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
      TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
      TNT1 A 0 A_JumpIfInventory("BWgrenade",1,"LaunchGrenade")
      TNT1 A 0 A_JumpIfInventory("Meleeattack",1,"KnifeAttack")
      Loop

    RealReadyEmptyMag:
      M1GG J 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("ChangeMelee",1,"NewMelee")
      TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
      TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
      TNT1 A 0 A_JumpIfInventory("BWgrenade",1,"LaunchGrenade")
      tnt1 a 0 A_JumpIfInventory("Reloading",1,"Reload")
      TNT1 A 0 A_JumpIfInventory("Meleeattack",1,"KnifeAttack")
      Loop

    Deselect:
      TNT1 A 0 A_TakeInventory("ChangeMelee",1)
      TNT1 A 0 A_Takeinventory("Zoomed",1)
      TNT1 A 0 A_Takeinventory("ADSmode",1)
      TNT1 A 0 A_Takeinventory("Meleeattack",1)
      TNT1 A 0 A_Takeinventory("BWgrenade",1)
      TNT1 A 0 A_Takeinventory("Reloading",1)
      TNT1 A 0 A_ZoomFactor(1.0)
      M1GS HGFEDCBA 1 
      TNT1 A 0 A_Lower
      Wait

    Select:
      TNT1 A 0 A_Raise
      Wait

    AltFire:
      Goto RealReady

    UnZoom:
      Goto RealReady

    NoAmmo:
      TNT1 A 0 A_Print("No ammo!")
      TNT1 A 0 A_PlaySound("Weapons/Empty")
      TNT1 A 0 A_Takeinventory("Zoomed",1)
      TNT1 A 0 A_ZoomFactor(1.0)
      TNT1 A 0 A_Takeinventory("ADSmode",1)
      Goto RealReady

    Fire:
      TNT1 A 0 A_JumpIfInventory("M1Load",1,1)
      Goto Autoreload

      TNT1 A 0 A_Takeinventory("Reloading",1)
      TNT1 AAAA 0
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_PlaySound("Weapons/Garand")
      TNT1 A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,5)
      TNT1 A 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
      TNT1 A 0 A_FireCustomMissile("GarandFlash",0,0,0,0)
      TNT1 A 0 A_Takeinventory("M1LOAD",1)
      M1GG B 1 Bright 
      TNT1 A 0 A_FireBullets(4,5,0,30,"RiflePuff")
      TNT1 A 0 A_SetPitch(pitch - 5.0)
      TNT1 A 0 A_FireCustomMissile("RifleCaseSpawn",5,0,8,-9)
      M1GG CDE 1

      TNT1 A 0 A_JumpIfInventory("M1LOAD",1,3)
      TNT1 A 0 A_PlaySound("Weapons/Garand/Ping")
      TNT1 A 0 A_FireCustomMissile("k98stripEmpty",0,0,4,5)
      Goto RealReadyEmptyMag

      M1GG FGHI 1
      Goto RealReady 

    Autoreload:
      TNT1 A 0 {
        return A_JumpIf(GetCVar("sv_autoreload"),"Reload");
      }
      TNT1 A 0 A_JumpIfInventory("M1CLIP",1,2)
      TNT1 A 0 A_Print("No ammo!")
      Goto RealReadyEmptyMag

      TNT1 A 0 A_Print("Reload!")
      TNT1 A 0 A_PlaySound("MAGPICK1")
      Goto RealReadyEmptyMag

    Spawn:
      gapk A -1
      Stop

    Reload:
      TNT1 A 0 A_ZoomFactor(1.0)
      TNT1 A 0 A_Takeinventory("Reloading",1)
      TNT1 A 0 A_Takeinventory("ADSmode",1)
      TNT1 A 0 A_Takeinventory("Zoomed",1)
      TNT1 A 0 A_JumpIfInventory("M1LOAD",8,"InsertBullets")//Even if is impossible automactly reload this weapon.
      TNT1 A 0 A_JumpIfInventory("M1CLIP",1,3)
      Goto NoAmmo
      TNT1 AAA 0
      M1GR ABCDEF 1
      TNT1 A 0 A_PLAYSOUND("M1LOAD")
      M1GR GHIJKL 2 
      M1GR MNOPQ 1
      Goto InsertBullets

    InsertBullets:
      TNT1 AAAA 0
      TNT1 A 0 A_JumpIfInventory("M1LOAD",8,"RealReady")
      TNT1 A 0 A_JumpIfInventory("M1CLIP",1,"AmmoIn")
      Goto RealReady

    AmmoIn:
      TNT1 A 0 A_Giveinventory("M1LOAD",1)
      TNT1 A 0 A_Takeinventory("M1CLIP",1)
      Goto InsertBullets

    DoKick:
      Goto RealReady

		Taunt:
			TNT1 A 0 A_PlaySound("FUCK", 1)
			TNT1 A 0 A_AlertMonsters
      TNT1 A 15 A_Takeinventory("Taunting",1)
			TNT1 A 5
			Goto RealReady

    LaunchGrenade:
      TNT1 A 0
      TNT1 A 0 A_TakeInventory("LaunchGrenade", 1)
      TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, 1)
      Goto NoGrenade

      TNT1 A 0 A_playSound("Pick/granate")
      TNT1 A 0 A_Jump(90,2)
      TNT1 A 0 A_PlaySound("*grenadecall", CHAN_VOICE)

      TNT1 AAAA 2

      TNT1 A 0 A_PLaySound ("Launch/granate")
      TNT1 A 0 A_FireCustomMissile("HandGrenade2", random(-3,3), 1, 0, 0, 0, 0)
      TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
      TNT1 A 1 
      TNT1 A 0 A_TakeInventory("LaunchGrenade", 1)
      TNT1 A 0 A_Takeinventory("BWgrenade",1)
      Goto RealReady

    NoGrenade:
      TNT1 A 0
      TNT1 A 0 A_Takeinventory("Zoomed",1)
      TNT1 A 0 A_Takeinventory("ADSmode",1)
      TNT1 A 0 A_ZoomFactor(1.0)
      TNT1 A 0 A_Print("No Grenades Left!")
      TNT1 A 0 A_Takeinventory("BWgrenade",1)
      Goto RealReady

    NewMelee:
      Goto RealReady

    NewMelee2:
      Goto RealReady

    NoAxeIn:
      Goto RealReady

    NewMelee3:
      Goto RealReady

    KnifeAttack:
      GOTO RealREADY

    AxeAttackState:
      Goto RealReady

    AxeThrowState:
      Goto RealReady

    LastThrowState:
      Goto RealReady

    NoAmmoForAxe:
      Goto RealReady
  }
}

ACTOR m1load : Ammo {
  Inventory.Amount 0
  Inventory.MaxAmount 8
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 8
}

ACTOR m1clip : Ammo {
  Inventory.PickupSound "pickups/ammo"
  Inventory.PickupMessage "M1 Garand's clip"
  Inventory.Amount 8
  Inventory.MaxAmount 200
  Ammo.BackpackAmount 16
  Ammo.BackpackMaxAmount 200
  Mass 10000
  Inventory.Icon "MAUSB0"

  States {
    Spawn:
      KARM A -1
      Loop
  }
}